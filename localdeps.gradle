ext.runCommand = { dir, command, arguments ->
    new ByteArrayOutputStream().withStream { os ->
        exec {
            workingDir = dir
            executable = command
            args = arguments  
            standardOutput = os
        }
        return os.toString()
    }
}


ext.git = { repo, args ->
    if (repo == null) repo = '.'
    return runCommand(repo, 'git', args)
}
/*
 * The localdeps object takes care of setting up our home-grown dependencies
 * so that they can be compiled as source code. This allows us to, at competitions,
 * build our code with local changes to any of our libraries, instead of having to
 * push and pull to git and maven.
 *
 * This is an extremely rudimentary solution that often requires multiple builds to be
 * successful.
 */

ext.localdeps = [
    /* The list of local dependencies. This should be set before any functions are run. */
    list: ["SwerveIO", "JLimelight", "LibOI"],
    /*
     * The repoPrefix is the URL from which all the above dependencies are pulled.
     * The format for dependencies is "$repoPrefix/$item" Where $item is the dependency
     * in the list above.
     */
    repoPrefix: "https://github.com/jordanbancino",

    /* Run this in settings.gradle */
    settingsSetup: {
        localdeps.list.eachWithIndex { item, index ->
            println "Fetching preemptively: $item"
            localdeps.syncLocalDependency("${localdeps.repoPrefix}/$item", item)
            localdeps.registerProjectIfExists(item)
        }
    },

    /* Run this in build.gradle */
    buildSetup: {
        localdeps.list.eachWithIndex { item, index -> 
            if (project.hasProperty("useLocal$item") || project.hasProperty("allLocal")) {
                println "Using local copy of $item."
                dependencies.implementation project(":$item")
                tasks.jar.dependsOn("$item:jar")
            }
        }
    },

    /* Register the given directory as a gradle project, only if it exists. */
    registerProjectIfExists: { proj ->
        if (file(proj).exists()) {
            include proj
        }
    },

    /* clone or pull the given git repository. URL is the remote, name is the local folder name.*/
    syncLocalDependency: { url, name ->
        def repoDir = file("$name")
        if (repoDir.exists()) {
            if (netIsAvailable()) {
                git(repoDir, ['pull'])
            } else {
                println "Warning: Unable to pull latest $name."
            }
        } else {
            git(null, ['clone', url, name])
        }
    }
]

/**
 * Check if internet is available by trying to reach the repoPrefix.
 *
 * @return Whether or not the remote URL is accessible after a 1 second wait period.
 */
private boolean netIsAvailable() {
    try {
        final URL url = new URL(localdeps.repoPrefix);
        final URLConnection conn = url.openConnection();
        conn.setConnectTimeout(1000);
        conn.connect();
        conn.getInputStream().close();
        return true;
    } catch (MalformedURLException e) {
        throw new RuntimeException(e);
    } catch (IOException e) {
        return false;
    }
}