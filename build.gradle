plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2020.1.2"
    id "org.ajoberstar.grgit" version "4.0.1"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def ROBOT_MAIN_CLASS = "frc.robot.Main"

deploy {
    targets {
        roboRIO("roborio") {
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        frcJavaArtifact('frcJava') {
            targets << "roborio"
            debug = frc.getDebugOrDefault(false)
        }
        fileTreeArtifact('frcStaticFileDeploy') {
            files = fileTree(dir: 'src/main/deploy')
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

def includeDesktopSupport = false

repositories {
    maven {
        url 'https://dump.bancino.net/maven'
    }
}

dependencies {
    /* WPILib Dependencies, don't modify these. */
    implementation wpi.deps.wpilib()
    nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)


    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

    testImplementation 'junit:junit:4.12'

    simulation wpi.deps.sim.gui(wpi.platforms.desktop, false)

    /* Add our dependencies here. */

    implementation 'net.bancino.robotics:JLimelight:1.0.3'

    /* SwerveIO is updated very often, so we usually prefer to download a copy of it and depend on a local copy. */
    if (project.hasProperty('cloneSwerveIO')) {
        def repoDir = file("$projectDir/SwerveIO")
        if (repoDir.exists()) {
            if (netIsAvailable()) {
                def repo = grgit.open(dir: repoDir)
                repo.pull()
                repo.close()
            } else {
                println 'Warning: Unable to pull latest SwerveIO.'
            }
        } else {
            def repo = grgit.clone {
                dir = "$projectDir/SwerveIO"
                uri = "https://github.com/Team6090/SwerveIO"
            }
            repo.close()
        }
        implementation project(":SwerveIO")
    } else {
        implementation 'net.bancino.robotics:SwerveIO:1.3.0'
    }
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
}

private static boolean netIsAvailable() {
    try {
        final URL url = new URL("https://github.com");
        final URLConnection conn = url.openConnection();
        conn.setConnectTimeout(1000);
        conn.connect();
        conn.getInputStream().close();
        return true;
    } catch (MalformedURLException e) {
        throw new RuntimeException(e);
    } catch (IOException e) {
        return false;
    }
}